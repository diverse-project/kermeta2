using kermeta::standard::*
using kermeta::io::StdIO => stdio
using ^org::kermeta::language::structure::*
using transform::*

/*
 * Generates Yuml code source for each Package.
 */
package YumlPackageGenerator{

	class YumlPackageVisitor inherits Abstract_PackageVisitor{
	
		attribute fileContent : StringBuffer
		attribute outputFolderUrl : String
		
		operation setOutputfolder(ofp : String) : Void is do
			self.outputFolderUrl := ofp
		end

		operation visitPackage (p: Package ): Void is do
		
			fileContent := StringBuffer.new
			
			p.ownedTypeDefinition.each{ otd | otd.accept(self) }	
			
			kermeta::io::FileIO.writeTextFile((outputFolderUrl+"/"+p.getQualifiedName()+".yuml").toURI(), fileContent.toString())
						
			p.nestedPackage.each { subPack | subPack.accept(self) }		
			
		end
	
		operation visitTypeDefinition (td: TypeDefinition ): Void is do
			//Nothing
		end
		
		operation visitClassDefinition (cd: ClassDefinition ): Void is do
		
			fileContent.append("[" + cd.name + "{bg:orange}],")
		
			//Inheritance
			cd.superType.each{e| 
						fileContent.append("[" + cd.name + "]" + "-^[" + e.getTypeDef() + "],") 
					}
					
			//Properties
			//cd.ownedAttribute.each{oa |
			//			oa.accept(self)
			//			}
		end
		
		operation visitProperty (p: Property ): Void is do
			//We want assocation with classes only. Enumeration types for example will not appear.
			if(p.type.isInstanceOf(org::kermeta::language::structure::Class)) then
				if(p.isComposite) then
					fileContent.append("[" + p.owningClass.name + "]" + "++->[" + p.type.getTypeDef() + "],")
				else
					fileContent.append("[" + p.owningClass.name + "]" + "->[" + p.type.getTypeDef() + "],")
				end			
			end
		end
		
		operation visitType (t: Type ): Void is do
			//Nothing
		end
		
		operation visitClass (c: Class ): Void is do
			//Nothing
		end

	}
	
	/*
	 * Entry point
	 */
	class YumlPackageTransform inherits Abstract_Transformation{
	
		attribute outputFolderUrl : String
	
		operation setOutputfolder(ofp : String) : Void is do
			self.outputFolderUrl := ofp
		end
	
		operation transform ( input : Abstract_VisitableElement ) : Object is do
		
			var visitor : YumlPackageVisitor := YumlPackageVisitor.new
			visitor.setOutputfolder(self.outputFolderUrl)
			input.accept(visitor)
			result := ""
		
		end
	
	}
	
}