using kermeta::standard::*
using kermeta::io::StdIO => stdio
using ^org::kermeta::language::structure::*
using transform::*

package YumlClassGenerator{
	class YumlClassVisitor inherits Abstract_PackageVisitor	{
	
		attribute classContext : Set<String>
		attribute fileContent : StringBuffer
		attribute path : String
		attribute outputFolderUrl : String
	
		operation setOutputfolder(ofp : String) : Void is do
			self.outputFolderUrl := ofp
		end

		operation visitPackage (p: Package ): Void is do
		
			path := p.getQualifiedName().replaceAll("::","/")
			
			p.ownedTypeDefinition.each{ otd | 
					otd.accept(self) 
					}					
			p.nestedPackage.each { subPack | subPack.accept(self) }		
			
		end
	
		operation visitTypeDefinition (td: TypeDefinition ): Void is do
			//Nothing
		end

		operation visitClassDefinition (cd: ClassDefinition ): Void is do
		
			//neighbours classes
			classContext := Set<String>.new
		
			fileContent := StringBuffer.new
			fileContent.append("[" + cd.name + "{bg:orange}],")
		
			/*
			 * First get all links with neighbours
			 */
			
			classContext.add(cd.name)
			 
			//Inheritance
			cd.superType.each{e| 
						fileContent.append("[" + cd.name + "]" + "-^[" + e.getTypeDef() + "],") 
						classContext.add(e.getTypeDef())
					}
					
			//Properties
			cd.ownedAttribute.each{oa |
						oa.accept(self)
						}
						
			/*
			 * Then get all links between neighbours
			 */
			 
			 var visitedElem : Set<String> := Set<String>.new
			 visitedElem.add(cd.name)
			 
			 //Inheritance
			cd.superType.each{e| 
						if( e.isInstanceOf(org::kermeta::language::structure::Class)) then
							var cl : Class := e.asType(org::kermeta::language::structure::Class) 
							visitClass(cl)
							visitedElem.add(cl.name)
						end
					}
					
			//Properties
			cd.ownedAttribute.each{oa |
						if( oa.type.isInstanceOf(org::kermeta::language::structure::Class)) then
							var cl : Class := oa.type.asType(org::kermeta::language::structure::Class) 
							//Do not follow links with itself
							if(not visitedElem.exists{elemName | elemName.equals(cl.name)}) then
								visitClass(cl)
								visitedElem.add(cl.name)
							end
						end
					}
					
			/*
			 * Store the result
			 */
			kermeta::io::FileIO.writeTextFile((outputFolderUrl+"/"+path+"/"+cd.name+".yuml").toURI(), fileContent.toString())
		end
		
		operation visitProperty (p: Property ): Void is do
			//We want assocation with classes only. Enumeration types for example will not appear.
			if(p.type.isInstanceOf(org::kermeta::language::structure::Class)) then
				
				if(p.isComposite) then
					fileContent.append("[" + p.owningClass.name + "]" + "++->[" + p.type.getTypeDef() + "],")
				else
					fileContent.append("[" + p.owningClass.name + "]" + "->[" + p.type.getTypeDef() + "],")
				end
				
				classContext.add(p.type.getTypeDef())
			end			
			
		end
		
		operation visitType (t: Type ): Void is do
			//Nothing
		end
		
		/*
		 * Get all links between the Class and the classContext
		 */
		operation visitClass (c: Class ): Void is do
			classContext.each{cc |
					//Inheritance
					c.superClass.each{e| 
								if( e.getTypeDef() == cc) then
									fileContent.append("[" + c.name + "]" + "-^[" + e.getTypeDef() + "],") 
								end
							}
							
					//Properties
					c.ownedAttribute.each{oa |
								if(oa.type.getTypeDef() == cc) then
									if(oa.isComposite) then
										fileContent.append("[" + c.name + "]" + "++->[" + oa.type.getTypeDef() + "],")
									else
										fileContent.append("[" + c.name + "]" + "->[" + oa.type.getTypeDef() + "],")
									end
								end
							}
					}
		end
	}
	
	/*
	 * Entry point
	 */
	class YumlClassTransform inherits Abstract_Transformation{
	
		attribute outputFolderUrl : String
	
		operation setOutputfolder(ofp : String) : Void is do
			self.outputFolderUrl := ofp
		end
	
		operation transform ( input : Abstract_VisitableElement ) : Object is do
			var visitor : YumlClassVisitor := YumlClassVisitor.new
			visitor.setOutputfolder(outputFolderUrl)
			input.accept(visitor)
			result := ""
		
		end
	
	}
}