using kermeta::standard::*
using kermeta::io::StdIO => stdio

using kermeta::emfpersistence::*

using logo_interpreter::Context
using logoASM::LogoProgram
using kmLogo::controler::TurtleControler

package org::kermeta::language::sample::logo::interpreter {
	class MainClass {
	
		attribute context 	: Context
		reference root 		: LogoProgram
	
		@main "" // declares that operation as a possible entry point of the program
		operation mainOperation(inputLogofile : String) : Void is do
			
			context := Context.new
			context.initialize
			// load the program
			stdio.writeln("Loading file")
			root := loadProgram(inputLogofile)
			stdio.writeln("File loaded")
			
			// create and initialize the GUI
			var controler : TurtleControler init TurtleControler.new
			controler.initialize(context.turtle)
			
			// Run interpretation
			stdio.writeln("Interpretation running")
			root.eval(context)
			stdio.writeln("Ended Interpretation")
			
			// update the GUI
			controler.modelChanged
									
		end
		
		operation loadProgram(logoFile : String) : LogoProgram is do   
			var resource : Resource
			if(logoFile.endsWith(".logo")) then
				stdio.writeln("Loading xText resource...")				
				resource ?= extern org::kermeta::language::sample::logo::interpreter::xtextwrapper::XTextResourceWrapper.getLogoXTextResource(logoFile)
			else          
				stdio.writeln("Loading XMI resource...")
				var repository : ResourceSet init ResourceSet.new
				resource := repository.createResource(logoFile, "platform:/resource/org.kermeta.kmlogo.logoasm.model/model/ASMLogo.ecore")
				resource.load(void)
			end      
			result ?= resource.getContents().one
		end
	}
}