using kermeta::standard::*
using kermeta::io::StdIO => stdio
using kermeta::io::FileIO => FileIO

using kermeta::emfpersistence::*
using ecore::*
using ecore_aspectgenerator::Context
using ecore_aspectgenerator::KmtFileGeneration

package org::kermeta::language::aspectgenerator{
	class AspectGenerator
	{					
		@main ""
	    operation aspectGeneratorTestMain() : Void is do
	        var ecoreFile		: String init "/src/test/Kermeta.ecore"

	        var userDefined		: OrderedSet[String] init OrderedSet[String].new
	        var newClass		: OrderedSet[String] init OrderedSet[String].new
	        var visitor			: OrderedSet[String] init OrderedSet[String].new
	        var test			: OrderedSet[String] init OrderedSet[String].new
	        
	        userDefined.add("context : Context")
	        userDefined.add("name : String")
	      	newClass.add("Context")
	        
	        visitor.add("v : Visitor")
	        
	        stdio.writeln("generation of a compiler project")
	        generateCompilerProject("Compiler", "platform:/resource/org.kermeta.language.aspectgenerator/target/result", ecoreFile, "org.kermeta.language.aspectgenerator")
	        
	        stdio.writeln("generation of a interpret project")
	        generateInterpreterProject("Interpreter", "platform:/resource/org.kermeta.language.aspectgenerator/target/result", ecoreFile, "org.kermeta.language.aspectgenerator")
	        
	        stdio.writeln("generation of a visitor pattern project")
	        generateVisitorProject("visit", "platform:/resource/org.kermeta.language.aspectgenerator/target/result", ecoreFile, "org.kermeta.language.aspectgenerator")
	        
	        stdio.writeln("generation of a user defined project")
	        generateCustomizeProject("userdefined", "platform:/resource/org.kermeta.language.aspectgenerator/target/result", ecoreFile, "org.kermeta.language.aspectgenerator", "userDefined", "Integer", userDefined, newClass)
	        
	        stdio.writeln("generation of a aspect project")
	        generateAspectProject("aspect", "platform:/resource/org.kermeta.language.aspectgenerator/target/result", ecoreFile, "org.kermeta.language.aspectgenerator")
	        
	        stdio.writeln("generation of a none project")
	        generateNone("none", "platform:/resource/org.kermeta.language.aspectgenerator/target/result", ecoreFile, "org.kermeta.language.aspectgenerator")
	        
	    end
	        
		operation loadEcoreModel(ecoreFile : String) : Collection[EPackage] is do             
			var repository : ResourceSet init ResourceSet.new
			var resource : Resource init repository.createResource(ecoreFile, "platform:/plugin/org.eclipse.emf.ecore/model/Ecore.ecore")
			
			resource.load(void)      
			result := OrderedSet[EPackage].new
			resource.getContents.each{next |
				if (next.isInstanceOf(EPackage)) then                 
					result.add(next.asType(EPackage))
				end
			}			
		end
		
		@main "generate a compiler"
		operation generateCompilerProject (nameProject : String, folderLocation : String, ecoreFile : String, ecoreProjectPath : String) : Void is do
			var inputPackages	: Collection[EPackage] init loadEcoreModel("platform:/resource/" + ecoreProjectPath + ecoreFile)
	        var context 		: Context := Context.new
	        var kmtFile 		: KmtFileGeneration init KmtFileGeneration.new.initialize(context)
		
		
			var operationParams	: OrderedSet[String] init OrderedSet[String].new
			operationParams.add("context : Context")
			
			//compile
	        context.initialize("compiler", nameProject, folderLocation, ecoreFile, ecoreProjectPath, "compile", "Void", operationParams)
	        inputPackages.each{p | p.generateAspect(context)}
	        kmtFile.writeNewClass("Context")
	        kmtFile.writeProjectKp
		end
		
		@main "generate an interpreter"
		operation generateInterpreterProject (nameProject : String, folderLocation : String, ecoreFile : String, ecoreProjectPath : String) : Void is do
			var inputPackages	: Collection[EPackage] init loadEcoreModel("platform:/resource/" + ecoreProjectPath + ecoreFile)
	        var context 		: Context := Context.new
	        var kmtFile 		: KmtFileGeneration init KmtFileGeneration.new.initialize(context)
	        
			//useless
			var operationParams	: OrderedSet[String] init OrderedSet[String].new
			
			//interpret
	        context.initialize("interpreter", nameProject, folderLocation, ecoreFile, ecoreProjectPath, "interpret", "Void", operationParams)
	        inputPackages.each{p | p.generateAspect(context)}
	        kmtFile.writeProjectKp
		end
		
		operation generateCustomizeProject (nameProject : String, folderLocation : String, ecoreFile : String, ecoreProjectPath : String, operationName : String, operationReturnType : String, operationParams : OrderedSet[String], listNewClass : OrderedSet[String]) : Void is do
			var inputPackages	: Collection[EPackage] init loadEcoreModel("platform:/resource/" + ecoreProjectPath + ecoreFile)
	        var context 		: Context := Context.new
	        var kmtFile 		: KmtFileGeneration init KmtFileGeneration.new.initialize(context)
	        
	        //customize
	        context.initialize("userDefined", nameProject, folderLocation, ecoreFile, ecoreProjectPath, operationName,operationReturnType, operationParams)
	        inputPackages.each{p | p.generateAspect(context)}
	        listNewClass.each{ nc | kmtFile.writeNewClass(nc)}
	        kmtFile.writeProjectKp
		end
		
		@main "generate a visitor as an object view"
		operation generateVisitorProject (nameProject : String, folderLocation : String, ecoreFile : String, ecoreProjectPath : String) : Void is do
			var inputPackages	: Collection[EPackage] init loadEcoreModel("platform:/resource/" + ecoreProjectPath + ecoreFile)
	        var context 		: Context := Context.new
	        var kmtFile 		: KmtFileGeneration init KmtFileGeneration.new.initialize(context)
	        var visitor			: OrderedSet[String] init OrderedSet[String].new
	        visitor.add("v : Visitor")
	        
			//visitor pattern
	        context.initialize("visitor", nameProject, folderLocation, ecoreFile, ecoreProjectPath, "accept", "Void", visitor)
	        inputPackages.each{p | p.generateAspect(context)}
	        kmtFile.writeVisitorPattern
	        kmtFile.writeProjectKp
		end
		
		@main "generate a visitor as an aspect view"
		operation generateAspectProject (nameProject : String, folderLocation : String, ecoreFile : String, ecoreProjectPath : String) : Void is do
			var inputPackages	: Collection[EPackage] init loadEcoreModel("platform:/resource/" + ecoreProjectPath + ecoreFile)
	        var context 		: Context := Context.new
	        var kmtFile 		: KmtFileGeneration init KmtFileGeneration.new.initialize(context)
	        
			//useless
			var operationParams	: OrderedSet[String] init OrderedSet[String].new
	        
			//aspect
	        context.initialize("", nameProject, folderLocation, ecoreFile, ecoreProjectPath, "", "", operationParams)
	        inputPackages.each{p | p.generateAspect(context)}	
	        kmtFile.writeProjectKp
		end
		
		@main "generate no transformation"
		operation generateNone (nameProject : String, folderLocation : String, ecoreFile : String, ecoreProjectPath : String) : Void is do
			var context 		: Context := Context.new
	        var kmtFile 		: KmtFileGeneration init KmtFileGeneration.new.initialize(context)
			//useless
			var operationParams	: OrderedSet[String] init OrderedSet[String].new
	        
	        context.initialize("", nameProject, folderLocation, ecoreFile, ecoreProjectPath, "", "", operationParams)
	        kmtFile.writeProjectKp
		end
	}
}
