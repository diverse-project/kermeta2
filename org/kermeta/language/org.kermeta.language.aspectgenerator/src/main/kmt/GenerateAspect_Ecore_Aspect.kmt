using kermeta::standard::*
using org::kermeta::language::aspectgenerator::Context

package ecore{
	aspect class EPackage
	{
		operation generateAspect(context : Context) : Void is do
			context.addPackage(self.name)
			
			// Processing on the subpackages if it exists there
			self.eSubpackages.each{ p | p.generateAspect(context)}
			
			// Processing on the class of the package if it exists there
			if(self.eClassifiers.exists{c | c.isInstanceOf(EClass)}) then
				context.writePackage
				self.eClassifiers.each{ c | 
					if (c.isInstanceOf(EClass)) then
						c.asType(EClass).generateAspect(context)
					end}
				context.writeFileIO
			end
			
			context.deletePackage
		end
	}
	
	aspect class EClass
	{
		method generateAspect(context : Context) : Void is do
			context.addClass(self.name)
			context.writeClass(self.~abstract, self.name)
		end
	}
}